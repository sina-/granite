add_library(oni-core-graphics window.cpp shader.cpp batch-renderer-2d.cpp font-manager.cpp renderer-2d.cpp scene-manager.cpp debug-draw-box2d.cpp texture-manager.cpp)

target_compile_features(oni-core-graphics
        PUBLIC
            cxx_std_17
        )

target_compile_definitions(oni-core-graphics
        PUBLIC
            GLEW_NO_GLU
        )

target_include_directories(oni-core-graphics
        PUBLIC
            $<BUILD_INTERFACE:${oni_SOURCE_DIR}/inc>
            $<BUILD_INTERFACE:${oni_SOURCE_DIR}/lib/glfw/include>
            $<BUILD_INTERFACE:${oni_SOURCE_DIR}/lib/glew/include>
        PRIVATE SYSTEM
            $<BUILD_INTERFACE:${oni_SOURCE_DIR}/lib/freetype-gl/include>
            $<BUILD_INTERFACE:${oni_SOURCE_DIR}/lib/freeimage/Source>
            $<BUILD_INTERFACE:${oni_SOURCE_DIR}/lib/freetype2/include>
            $<BUILD_INTERFACE:${oni_SOURCE_DIR}/lib/ant-tweak-bar/include>
            $<BUILD_INTERFACE:${oni_SOURCE_DIR}/lib/box2d>
        )

find_library(ANT_TWEAK_BAR AntTweakBar PATHS ${oni_SOURCE_DIR}/lib/ant-tweak-bar/bin)
find_library(FREETYPE_GL freetype-gl PATHS ${oni_SOURCE_DIR}/lib/freetype-gl/bin)
find_library(FREETYPE_LIBRARY freetype PATHS ${oni_SOURCE_DIR}/lib/freetype2/builds)
find_library(FREEIMAGE_LIBRARY freeimage PATHS ${oni_SOURCE_DIR}/lib/freeimage)
find_library(GLFW_LIBRARY glfw3 PATHS ${oni_SOURCE_DIR}/lib/glfw/build/src)
find_library(GLEW_LIBRARY GLEW PATHS ${oni_SOURCE_DIR}/lib/glew/lib)

set(OpenGL_GL_PREFERENCE "GLVND")
find_package(OpenGL REQUIRED)
find_package(X11 REQUIRED)

target_link_libraries(oni-core-graphics
        PUBLIC
            ${GLFW_LIBRARY}
            ${GLEW_LIBRARY}
        PRIVATE
            # Not sure why this is needed, but bunch of GLFW linking fails without it
            ${CMAKE_DL_LIBS}
            ${X11_LIBRARIES}
            ${FREEIMAGE_LIBRARY}
            ${FREETYPE_GL}
            # NOTE: FreeType must be linked after freetype-gl because fucking g++
            ${FREETYPE_LIBRARY}
            ${OPENGL_LIBRARY}
            # NOTE: For some reason when building glfw from source these are not linked into the binary...
            Xinerama Xrandr Xcursor Xxf86vm
            ${ANT_TWEAK_BAR}
            oni-core-buffers
            oni-core-io
            oni-core-entities
        )

add_dependencies(oni-core-graphics
        oni-core-buffers
        oni-core-io)